/*
 * Copyright 2017 Sanjin Sehic
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'alioli'
version '0.1.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

final Properties local = new Properties()
local.load(project.rootProject.file('local.properties').newDataInputStream())

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'com.google.guava:guava:23.0'
    compile 'junit:junit:4.12'
    compile 'org.jetbrains:annotations:15.0'
    testCompile 'com.google.truth:truth:0.35'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        JCenter(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.name
            artifactId project.name
            version project.version
        }
    }
}

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.apikey')
    publications = ['JCenter']

    pkg {
        repo = 'alioli'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/saserr/alioli.git'

        version {
            name = project.version
            desc = "$project.name v$project.version"
            released  = new Date()
            vcsTag = "v$project.version"
        }
    }
}
